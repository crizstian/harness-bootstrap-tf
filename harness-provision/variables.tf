# harness variables
variable "harness_platform_api_key" {
  type      = string
  sensitive = true
}
variable "harness_platform_account_id" {
  type = string
}
variable "harness_platform_organizations" {
  type = map(any)
}
variable "harness_platform_delegates" {
  type    = map(any)
  default = {}
}
variable "harness_platform_github_connectors" {
  type    = map(any)
  default = {}
}
variable "harness_platform_docker_connectors" {
  type    = map(any)
  default = {}
}
variable "harness_platform_aws_connectors" {
  type    = map(any)
  default = {}
}
variable "harness_platform_pipelines" {
  type    = map(any)
  default = {}
}
variable "harness_platform_inputsets" {
  type    = map(any)
  default = {}
}
# other variables
variable "organization_prefix" {
  type    = string
  default = ""
}

# common vars
locals {
  common_tags = { tags = ["owner: ${var.organization_prefix}"] }
  common_schema = {
    org_id     = module.bootstrap_harness_account.organization[var.organization_prefix].org_id
    project_id = module.bootstrap_harness_account.organization[var.organization_prefix].seed_project_id
    suffix     = module.bootstrap_harness_account.organization[var.organization_prefix].suffix
  }
  git_suffix    = "_github_connector"
  seed_pipeline = var.harness_platform_pipelines["harness_seed_setup"]
}

# delegate locals
locals {
  common_schema_delegate = {
    description            = "Delegate deployed and generated by terraform harness provider"
    size                   = "SMALL"
    tags                   = ["owner: ${var.organization_prefix}"]
    clusterPermissionType  = "CLUSTER_ADMIN"
    customClusterNamespace = "harness-delegate-ng"
  }

  delegates = { for type, delegates in var.harness_platform_delegates : type => { for key, value in delegates : key => merge(value, local.common_schema_delegate) } }
}

# github connectors
locals {
  github_connectors = { for name, details in var.harness_platform_github_connectors : name => merge(
    details,
    local.common_tags,
    {
      validation_repo = details.connection_type == "Repo" ? "" : details.validation_repo
      org_id          = details.connection_type == "Repo" ? module.bootstrap_harness_account.organization[var.organization_prefix].org_id : try(details.org_id, "")
      project_id      = details.connection_type == "Repo" ? module.bootstrap_harness_account.organization[var.organization_prefix].seed_project_id : try(details.project_id, "")
      credentials = {
        http = {
          username     = details.credentials.http.username
          token_ref_id = try(details.credentials.http.token_ref_id, "")
        }
      }
      api_authentication = {
        token_ref = try(details.credentials.http.token_ref_id, "")
      }
  }) }
}

# connectors
locals {
  docker_connectors = { for name, details in var.harness_platform_docker_connectors : name => merge(details, local.common_tags) if details.enable }
  aws_connectors    = { for name, details in var.harness_platform_aws_connectors : name => merge(details, local.common_tags) if details.enable }
}

# k8s connectors
locals {
  k8s_connectors = merge([for type, delegates in var.harness_platform_delegates : {
    for key, value in delegates : key => merge(
      value,
      local.common_tags,
      {
        description        = "K8s Connector generated by terraform harness provider"
        delegate_selectors = [key]
      }
    )
    } if type == "k8s"
  ]...)
}

# seed pipelines
locals {
  seed_pipelines = { for org, values in var.harness_platform_organizations : "harness_seed_setup_${values.short_name}" => {
    pipeline = merge(
      { for key, value in local.seed_pipeline : key => value if key != "custom_template" },
      local.seed_pipeline.custom_template.pipeline,
      {
        vars = merge(
          local.seed_pipeline.custom_template.pipeline.vars,
          {
            org_id                  = module.bootstrap_harness_account.organization[org].org_id
            project_id              = module.bootstrap_harness_account.organization[org].seed_project_id
            suffix                  = module.bootstrap_harness_account.organization[org].suffix
            tf_provision_identifier = "tf_${org}"
            tf_backend_prefix       = org
            git_connector_ref       = module.bootstrap_harness_connectors.connectors.github_connectors["${values.short_name}${local.git_suffix}"].identifier
          }
        )
    })
    inputset = { for input, details in try(local.seed_pipeline.custom_template.inputset, {}) : input => merge(details) if details.enable }
    }
  }
}

# devsecops pipelines
locals {
  devsecops_pipelines = { for pipe, values in var.harness_platform_pipelines : pipe => {
    pipeline = merge(
      { for key, value in values : key => value if key != "custom_template" },
      values.custom_template.pipeline,
      {
        vars = merge(
          values.custom_template.pipeline.vars,
          local.common_schema,
          {
            git_connector_ref = module.bootstrap_harness_connectors.connectors.github_connectors["${values.custom_template.pipeline.vars.git_connector}${local.git_suffix}"].identifier
            service_ref       = module.bootstrap_harness_delegates.delegate_init.service_ref
            environment_ref   = module.bootstrap_harness_delegates.delegate_init.environment_ref
          }
        )
      }
    )
    inputset = { for input, details in try(values.custom_template.inputset, {}) : input => merge(details) if details.enable }
    } if pipe != "harness_seed_setup"
  }
}

# pipelines
locals {
  pipelines = merge(
    local.seed_pipelines,
    local.devsecops_pipelines
  )
}
